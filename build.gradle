buildscript {
    ext.kotlin_version = '1.4.31'
    repositories {
        maven {
            url "https://repo.osgeo.org/repository/release/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'kotlin'
apply from: "$project.rootDir/tools/script-grgit-version.gradle"

group 'kr.djgis'
version gitVersionName

repositories {
    maven {
        url "https://repo.osgeo.org/repository/release/"
    }
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    // compile -> implementation 으로 바꾸지 말 것!
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.10'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-core
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.10'
    // https://mvnrepository.com/artifact/mysql/mysql-connector-java
    compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.23'
    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.19'
    // https://mvnrepository.com/artifact/org.geotools/gt-main
    compile group: 'org.geotools', name: 'gt-main', version: '25-RC'
    // https://mvnrepository.com/artifact/org.geotools/gt-shapefile
    compile group: 'org.geotools', name: 'gt-shapefile', version: '25-RC'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-dbcp2
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.8.0'
    // https://ktlint.github.io/#getting-started
    ktlint "com.pinterest:ktlint:0.41.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

jar {
    archiveBaseName = 'GSS_ConvertSHP'
    manifest {
        attributes 'Main-Class': 'kr.djgis.shpbackup3.MainKt'
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.withType(Jar) {
    destinationDirectory = file("$rootDir/release/")
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
}
